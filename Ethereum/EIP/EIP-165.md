# EIP-165
#Ethereum/eip

## Abstract
1. 인터페이스가 식별되는 방법
2. 컨트랙트를 배포하면서 인터페이스들을 배포하는 방법
3. 컨트랙트가 ERC-165를 구현했는지의 여부를 확인하는 방법
4. 컨트랙트가 어떤 인터페이스들을 구현했는지를 확인하는 방법

## Motivation
ERC-20 토큰 인터페이스와 같은 표준 인터페이스들은 때때로 컨트랙트가 해당 인터페이스를 지원했는지의 여부를 조회하기에 유용하다. 만약 인터페이스를 지원한다면, 어떤 버전인지 까지 조회할 수 있다.
특히 ERC-20의 버전 식별자는 이미 제안되었다. 이 제안은 인터페이스의 개념을 표준화하고 인터페이스의 식별자(이름)를 표준화한다.

## Specification
### 인터페이스가 식별되는 방법
인터페이스는 Ethereum ABI에서 정의된  [Function Selector](bear://x-callback-url/open-note?id=8EA80E02-CE47-4607-81E3-7470AD5AD701-1163-0000417540F36B1C) 의 집합이다.  인터페이스 키워드는 리턴 타입과 가변성 그리고 이벤트까지 정의한다. 
인터페이스 식별자는 인터페이스 내의 모든 함수 선택자(4바이트의 콜 데이터)에 대해 XOR로 구현되어 있다. 아래의 코드를 보면 인터페이스 식별자를 계산하는 과정이 나와 있다.
```
pragma solidity ^0.4.20;

interface Solidity101 {
    function hello() external pure;
    function world(int) external pure;
}

contract Selector {
    function calculateSelector() public pure returns (bytes4) {
        Solidity101 i;
        return i.hello.selector ^ i.world.selector;
    }
}
```
> 인터페이스는 do not permit optional functions, therefore, the interface identity will not include them.  

위의 컨트랙트 코드를 Remix IDE에서 실행한 후 결과값을 살펴보면서 이해해보도록 한다. `i.hello.selector`와 `i.world.selector`의 hex값들을 먼저 살펴보고 실제로 XOR 연산을 진행한 값이 어떻게 나오는지 살펴보도록 한다.
* `i.hello.selector`: `0x19ff1d21`
* `i.world.selector`: `0xdf419679`
* `i.hello.selector ^ i.world.selector`: `0xc6be8b58`

이렇게 각 함수들에 대한 선택자(4바이트의 콜데이터)들을 전부 XOR 연산한 값으로 인터페이스가 적정 함수들을 다 구현하였는지를 확인한다.

### 컨트랙트가 인터페이스를 구현한 구현체를 배포하는 방법
ERC-165를 구현하는 컨트랙트는 다음과 같은 인터페이스를 구현해야 한다. (referred as ERC165.sol)
```
pragma solidity ^0.4.20;

interface ERC165 {
    /// @notice Query if a contract implements an interface
    /// @param interfaceID The interface identifier, as specified in ERC-165
    /// @dev Interface identification is specified in ERC-165. This function
    ///  uses less than 30,000 gas.
    /// @return `true` if the contract implements `interfaceID` and
    ///  `interfaceID` is not 0xffffffff, `false` otherwise
    function supportsInterface(bytes4 interfaceID) external view returns (bool);
}
```
ERC165 인터페이스에 대한 식별자는 `0x01ffc9a7`이다. 다음과 같은 과정을 통해 직접 계산해 볼 수 있다.
* `bytes4(keccack256('supportsInterface(bytes4)'))`
* 위에서 사용했던 `Selector` contract 내의 `calculateSelector()`를 통해 계산할 수도 있다.

따라서 구현된 컨트랙트는 `supportsInterface`함수를 가질 것이고, 이 함수의 리턴값은 아래와 같을 것이다.
* true when interfaceID is `0x01ffc9a7` (EIP165 interface)
* false when interfaceID is `0xffffffff`
* true for any other interfaceID this contract implements
* false for any other interfaceID
`supportsInterface`함수는 30,000 가스를 소비하고 bool 자료형으로 반드시 리턴한다. 이 함수를 구현하기 위해서는 몇개의 논리적인 방법이 존재한다.

### 컨트랙트가 ERC-165를 구현했는지 확인하는 방법
1. The source contract makes a STATICCALL to the destination address with input data: `0x01ffc9a701ffc9a700000000000000000000000000000000000000000000000000000000 `  and gas 30,000. 이것은 `contract.supportsInterface(0x01ffc9a7)`와 동일하다.
2. If the call fails return false, the destination contract does not implement ERC-165.
3. If the call returns true, a second call is made with input data `0x01ffc9a7ffffffff00000000000000000000000000000000000000000000000000000000 `.
4. If the second call failed or returns false, the destination contract does not implement ERC-165.
5. Otherwise it implements ERC-165.

### 컨트랙트가 특정 인터페이스를 구현했는지 확인하는 방법
1. 만약 ERC-165 인터페이스에 대한 검증이라면, 위에서 설명한 과정을 통해 확인한다.
2. ERC-165를 구현한 것이 아니라면, you will have to see what methods it uses the old-fashioned way.
3. ERC-165를 구현했다면 `supportsInterface(interfaceID)`만으로 확인할 수 있다.

### 하위 호환성
위에서 제시된 `0xffffffff` 매카니즘은 표준 이전에 정의된 컨트랙트(ERC-165를 구현하지 않은 컨트랙트)에서도 동작해야 한다.
ENS 또한 EIP를 구현했다.

## Test Cases
아래의 컨트랙트 코드는 다른 컨트랙트가 특정 인터페이스를 구현했는지 여부를 체크해준다.
```
pragma solidity ^0.4.20;

contract ERC165Query {
    bytes4 constant InvalidID = 0xffffffff;
    bytes4 constant ERC165ID = 0x01ffc9a7;

    function doesContractImplementInterface(address _contract, bytes4 _interfaceId) external view returns (bool) {
        uint256 success;
        uint256 result;

        (success, result) = noThrowCall(_contract, ERC165ID);
        if ((success==0)||(result==0)) {
            return false;
        }

        (success, result) = noThrowCall(_contract, InvalidID);
        if ((success==0)||(result!=0)) {
            return false;
        }

        (success, result) = noThrowCall(_contract, _interfaceId);
        if ((success==1)&&(result==1)) {
            return true;
        }
        return false;
    }

    function noThrowCall(address _contract, bytes4 _interfaceId) constant internal returns (uint256 success, uint256 result) {
        bytes4 erc165ID = ERC165ID;

        assembly {
                let x := mload(0x40)               // Find empty storage location using "free memory pointer"
                mstore(x, erc165ID)                // Place signature at begining of empty storage
                mstore(add(x, 0x04), _interfaceId) // Place first argument directly next to signature

                success := staticcall(
                                    30000,         // 30k gas
                                    _contract,     // To addr
                                    x,             // Inputs are stored at location x
                                    0x20,          // Inputs are 32 bytes long
                                    x,             // Store output over input (saves space)
                                    0x20)          // Outputs are 32 bytes long

                result := mload(x)                 // Load the result
        }
    }
}
```

#Todo
해석해야할 내용들이 아직 남아있다.